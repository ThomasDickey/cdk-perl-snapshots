dnl Process this file with autoconf to produce a configure script.
AC_REVISION($Revision: 1.4 $)
AC_PREREQ(2.13.20030927)
rm -f config.cache

dnl $Id: configure.in,v 1.4 2012/03/24 11:48:01 tom Exp $

AC_INIT(Cdk.xs)
AC_PREFIX_DEFAULT(/usr)

AC_MSG_CHECKING(for version used by Cdk.pm)
VERSION=`egrep '[[$]]VERSION[[ 	]][[ 	]]*=' $srcdir/Cdk.pm | sed -e 's/^[[^"]]*"//' -e 's/".*//'`
AC_MSG_RESULT($VERSION)
AC_SUBST(VERSION)

AC_PROG_CC
AC_CHECK_PROGS(PERL,[perl perl5],none)
if test "$PERL" = none
then
	AC_MSG_ERROR(no perl found)
fi

AC_MSG_CHECKING(if you want to use cdk5-config for C flags)
AC_ARG_WITH(cdk,
	[  --with-cdk              use cdk5-config for C flags],
	[with_cdk="cdk5-config"],
	[with_cdk=no])
AC_MSG_RESULT($with_cdk)

AC_MSG_CHECKING(if you want to use cdkw5-config for C flags)
AC_ARG_WITH(cdkw,
	[  --with-cdkw             use cdkw5-config for C flags],
	[with_cdk="cdkw5-config"],
	[with_cdk=no])
AC_MSG_RESULT($with_cdk)

if test $with_cdk = no
then
	AC_CHECK_HEADER(cdk.h,,[
		cf_save_CPPFLAGS="$CPPFLAGS"
		CPPFLAGS="$CPPFLAGS -I/usr/include/cdk"
		AC_CHECK_HEADER(cdk/cdk.h,[
			AC_DEFINE(HAVE_CDK_CDK_H)
		],[
			AC_MSG_ERROR(cannot find cdk header file)
		])
	])
	AC_CHECK_LIB(cdk,initCDKScreen,[LIBS="-lcdk $LIBS"],[
	AC_CHECK_LIB(cdkw,initCDKScreen,[LIBS="-lcdkw $LIBS"],[
	AC_MSG_ERROR(cannot find cdk library)])])

elif $with_cdk --version 2>/dev/null >/dev/null
then
	CFLAGS=`$with_cdk --cflags`
	LIBS=`$with_cdk --libs`
else
	AC_MSG_ERROR(cannot use $cdk_config script)
fi

AC_SUBST(LIBS)
AC_SUBST(CPPFLAGS)

###	output makefile script
AC_OUTPUT(Makefile.PL,[

# move confdefs.h aside while using perl, since it gets confused
test -f "confdefs.h" && mv confdefs.h confdefs.hdr
$PERL Makefile.PL
test -f "confdefs.hdr" && mv confdefs.hdr confdefs.h

# add to the distclean rule to cleanup files created by the configure script
cat >>Makefile <<CF_EOF
distclean ::
	rm -f Makefile.PL config.status config.cache config.log
CF_EOF
],[
PERL="$PERL"
])
